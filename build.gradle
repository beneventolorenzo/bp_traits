apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 3
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}

afterEvaluate {

    apply plugin: 'maven-publish'

    def env = System.getenv()
    def github_usr = env["GITHUB_USR"]
    def github_key = env["GITHUB_KEY"]

    def getVersionName = { ->
        return android.defaultConfig.versionName
    }

    def getArtifactId = { ->
        return "bp_traits"
    }

    publishing {

        publications {
            bar(MavenPublication) {
                groupId 'com.scitalys'
                artifactId getArtifactId()
                version getVersionName()
                artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.api.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Lorenzo1818/bp_traits")
                credentials {
                    username = github_usr // ?: System.getenv("GPR_USER")
                    password = github_key // ?: System.getenv("GPR_API_KEY")
                }
            }
        }

    }

}